plugins {
    id 'org.beryx.jlink'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

ext.log4jVersion = '2.19.0'

dependencies {
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    implementation "javax.xml.bind:jaxb-api:2.3.0"
    implementation 'com.twelvemonkeys.imageio:imageio-jpeg:3.5'
    implementation 'com.twelvemonkeys.imageio:imageio-tiff:3.5'
}

application {
    mainClass = 'org.beryx.modular.hello.Hello'
}

jar {
    manifest {
        attributes 'Implementation-Title': "modular-hello",
                'Main-Class': application.mainClass
    }
}

compileJava {
    options.compilerArgs = ['--module-path', classpath.asPath]
    classpath = files()
}

jlink {
    launcher {
        name = 'hello'
    }
    mergedModule {
        version = '5.4.3'
        requires 'java.naming';
        requires 'java.xml';
        requires 'java.datatransfer';
        requires 'java.management';
        requires 'java.scripting';
        requires 'java.desktop';

        uses 'org.apache.logging.log4j.spi.Provider';
        provides 'org.apache.logging.log4j.spi.Provider' with 'org.apache.logging.log4j.core.impl.Log4jProvider';
        provides 'javax.imageio.spi.ImageReaderSpi' with 'com.twelvemonkeys.imageio.plugins.jpeg.JPEGImageReaderSpi',
                'com.twelvemonkeys.imageio.plugins.tiff.BigTIFFImageReaderSpi',
                'com.twelvemonkeys.imageio.plugins.tiff.TIFFImageReaderSpi';
        provides 'javax.imageio.spi.ImageWriterSpi' with 'com.twelvemonkeys.imageio.plugins.jpeg.JPEGImageWriterSpi',
                'com.twelvemonkeys.imageio.plugins.tiff.TIFFImageWriterSpi';
    }
}

tasks.jlink.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/image/bin")
    }
}
